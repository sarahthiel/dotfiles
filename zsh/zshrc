# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.zsh

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# all of our zsh files
typeset -U config_files
config_files=($ZSH/*/*.zsh)

# load the path files
for file in ${(M)config_files:#*/first.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${${config_files:#*/first.zsh}:#*/completion.zsh}:#*/last.zsh}
do
  source $file
done

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

# load last.zsh
for file in ${(M)config_files:#*/last.zsh}
do
  source $file
done

unset config_files

tm